define S ["-"|":"];
define V [ a | e | i | o | u | ä | ë | ï | ö | ü | ā | ē | ī | ō | ū ];
define v [ a | e | i | o | u ];
define X [ "A" | "O" | "V" ];
define C [ {b'} | {ch} | {ch'} | h | j | k | {k'} | l | m | n | p | q | {q'} | r | s
                | t | {t'} | {tz} | {tz'} | w | x | y | 7 ];


define POS [
    "POS":0 "(":0
    [
        [
                {discoid}:{ket}
            |   {sitting}:{t'uy}
            |   {heaped}:{mul}
            |   {standing}:{tak'}
            |   {spherical}:{sur}
            |   {cylindrical}:{b'ol}
            |   {stopped}:{tan}
            |   {lonely}:{won}
            |   {reclining}:{q'oy}
            |   {uncovered}:{san}
            |   {straight}:{suk'}
            |   {vacant}:{tol}
            |   {united}:{tun}
            |   {hanging}:{xek}
            |   {blinking}:{yup}
            |   {naked}:{ch'an}
            |   {wet}:{ch'aq}
            |   {facedown}:{jup}
            |   {paired}:{k'ul}
            |   {bowed}:{pach}
            |   {faceup}:{pak'}
            |   {high}:{paq}
            |   {unfolded}:{patz'}
            |   {flat}:{per}
            |   {crosswise}:{q'e7}
            |   {tilted}:{tzal}
            |   {dangling}:{tzay}
            |   {crawling}:{chaq}
            |   {settled}:{jeq}
            |   {alive}:{k'as}
            |   {lengthwise}:{rab'}
            |   {crosslegged}:{sep}
            |   {kneeling}:{xuk}
            |   {longlegged}:{wak'}
            |   {hunched}:{wuq'}
            |   {unfinished}:{ch'eq}
            |   {crooked}:{jech'}
            |   {certain}:{jik}
            |   {thick}:{jil}
            |   {gaping}:{k'ab'}
            |   {round}:{k'ol}
            |   {frayed}:{kiy}
            |   {curled}:{kot}
            |   {extensive}:{lik'}
            |   {warped}:{pek'}
        ] 
        .p.   {?}:0 [C v C]
    ]
    ")":0
];

define TEMP [
        [POS .o.  [?* "l"]] "+temp":{-an}
    |   [POS .o. ~[?* "l"]] "+temp":{-Vl}
];

define PERM [
        POS "+perm":{-V1}
];

define POSTV [
        [POS "+fall":{-Vb'a7}]
    |   [POS "+port":{-e}]
];

define RTV [
    "RTV":0 "(":0
    [
        [       {make}:{b'an}
            |   {bury}:{muq}
            |   {see}:{il}
            |   {grab}:{chap}
            |   {sweep}:{mes}
            |   {buy}:{loq'}
            |   {shake}:{yik}
            |   {mix}:{yuj}
            |   {destroy}:{yoj}
            |   {raise}:{yak}
            |   {scold}:{yaj}
            |   {tie}:{xim}
            |   {construct}:{wok}
            |   {decorate}:{wiq}
            |   {tear}:{wex}
            |   {grind}:{waq}
            |   {bother}:{tzur}
            |   {kindle}:{tzij}
            |   {rip}:{tzer}
            |   {drop}:{tzaq}
            |   {cook}:{tzak}
            |   {paint}:{tz'aj}
            |   {disassemble}:{tzur}
            |   {open}:{tor}
            |   {pay}:{toj}
            |   {help}:{to7}
            |   {eat}:{tij}
            |   {bite}:{ti7}
            |   {puncture}:{teq'}
            |   {separate}:{tas}
            |   {send}:{taq}
            |   {ask}:{tah}
            |   {sew}:{t'is}
            |   {wipe}:{su7}
            |   {deceive}:{sub'}
            |   {untangle}:{sol}
            |   {wound}:{sok}
            |   {lose}:{sach}
            |   {lick}:{riq'}
            |   {find}:{riq}
            |   {lend}:{qaj}
            |   {chip}:{q'ip}
            |   {stain}:{q'o7}
            |   {cut}:{q'at}
            |   {dice}:{pus}
            |   {wrap}:{pis}
            |   {butcher}:{pil}
            |   {squish}:{pich'}
            |   {weigh}:{paj}
            |   {assemble}:{nuk'}
            |   {feel}:{na7}
            |   {soak}:{mu7}
            |   {collect}:{mol}
            |   {hire}:{mok}
            |   {heat}:{miq'}
            |   {fold}:{mej}
            |   {tortilla}:{lej}
            |   {forgive}:{kuy}
            |   {preserve}:{kol}
            |   {use}:{koj}
            |   {cut}:{ket}
            |   {weave}:{kem}
            |   {show}:{k'ut}
            |   {dig}:{k'ot}
            |   {finish}:{k'is}
            |   {hurl}:{k'aq}
            |   {change}:{k'ex}
            |   {degrain}:{k'et}
            |   {burn}:{k'at}
            |   {bring}:{k'am}
            |   {scrape}:{juq}
            |   {shave}:{jos}
            |   {tear}:{jis}
            |   {pull}:{jek'}
            |   {divide}:{jach}
            |   {water}:{ja7}
            |   {explain}:{ch'ob'}
            |   {defeat}:{ch'ak}
            |   {hit}:{ch'ay}
            |   {wash}:{ch'aj}
            |   {roast}:{b'ol}
            |   {delay}:{b'ey}
        ] 
        .p.   {?}:0 [C v C]
    ]
    ")":0
];

define DTV [
    "DTV":0 "(":0
    [
            {cure}:{kuna}
        |   {carry}:{eqa}
        |   {bear}:{ala}
        |   {say}:{b'i}
        |   {mourn}:{b'iso}
        |   {sing}:{b'ixa}
        |   {address}:{ch'ab'e}
        |   {protect}:{chaji}
        |   {work}:{chaku}
        |   {designate}:{chilb'e}
        |   {use}:{choko}
        |   {resolve}:{choma}
        |   {inherit}:{echb'e}
        |   {lug}:{ere}
        |   {remove}:{ehsa}
        |   {measure}:{ehta}
        |   {bewitch}:{itzi}
        |   {await}:{iye}
        |   {winnow}:{jopi}
        |   {awaken}:{k'asu}
        |   {engender}:{k'ajola}
        |   {excavate}:{k'oti}
        |   {grind}:{ke'e}
        |   {mount}:{keje}
        |   {love}:{loq'o}
        |   {start}:{maji}
        |   {wrong}:{maku}
        |   {admire}:{mayi}
        |   {shade}:{muja}
        |   {gather}:{muli}
        |   {obey}:{nima}
        |   {plan}:{no7ji}
        |   {serve}:{patani}
        |   {fry}:{poli}
        |   {pop}:{poq'i}
        |   {ignite}:{poro}
        |   {embrace}:{q'alu}
        |   {impede}:{q'ate}
        |   {pass}:{q'axa}
        |   {cover}:{q'u7u}
        |   {lower}:{qasa}
        |   {trim}:{qupi}
        |   {covet}:{rayi}
        |   {call}:{sik'i}
        |   {gift}:{sipa}
        |   {swell}:{sipo}
        |   {visit}:{soli}
        |   {tamale}:{sub'a}
        |   {spin}:{suti}
        |   {till}:{taji}
        |   {continue}:{taqe}
        |   {shoulder}:{tele}
        |   {pursue}:{terne7e}
        |   {teach}:{tijo}
        |   {court}:{tz'ono}
        |   {return}:{tzali}
        |   {block}:{tzapi}
        |   {mock}:{tze7e}
        |   {recount}:{tzijo}
        |   {liberate}:{tzoqopi}
        |   {hunt}:{tzuku}
        |   {bother}:{tzuru}
        |   {drink}:{uk'a}
        |   {devour}:{web'e}
        |   {collapse}:{wuli}
        |   {vomit}:{xawa}
        |   {scare}:{xib'i}
    ]
    ")":0
] | POSTV;

define IV [
    "IV":0 "(":0
    [
            {enter}:{ok}
        |   {walk}:{b'ihn}
    ]
    ")":0
] | TEMP ;

define N1 [
    "N":0 "(":0
    [
            {avocado}:{ooj}
        |   {face}:{wach}
        |   {horse}:{keej}
        |   {loom}:{keem}
        |   {people}:{winaq}
        |   {tortilla}:{leej}
        |   {mouth}:{chii7}
        |   {sky}:{kaaj}
        |   {word}:{tziij}
        |   {soul}:{ahnimah}
        |   {nation}:{amaaq'}
        |   {trash}:{mees}
        |   {weeds}:{q'ayees}
        |   {precipice}:{swaan}
        |   {corn}:{ixiim}
        |   {eye}:{b'aq'wach}
        |   {sandal}:{xajaab'}
# Genuinely irregular pluralizable N1s are listed out here.
# Regular or semiregular ones go in the class below.
        |   {boy}:{alah}
        |   {boy}:{ala} "+pl":{-b'oom}
        |   {girl}:{alih}
        |   {girl}:{al} "+pl":{-tomaab'}
        |   {man}:{achih}
        |   {man}:{achi} "+pl":{-jaab'}
    ]
    ")":0
];

define N1pluralizable [
    "N":0 "(":0
    [
            {child}:{ak'aal-aab'}
        |   {woman}:{ixoq-iib'}
        |   {worker}:{ajchaak-iib'}
        |   {priest}:{ajq'iij-aab'}
    ]
    ")":0
];

define N1a [
    "N":0 "(":0
    [
        {bean}:{kinaq'}
    ]
    ")":0
];

define N2 [
    "N":0 "(":0
    [       {bone}:{b'aaq-iil}
        |   {blood}:{kik'-eel}
        |   {shadow}:{mu7j-aal}
        |   {hide}:{tz'u7uum-aal}
    ]
    ")":0
];

define N3 [
    "N":0 "(":0
    [
            {foot}:{aqan-aaj}
        |   {hand}:{q'ab'-aaj}
        |   {stomach}:{paam-aaj}
        |   {tooth}:{ware'-aaj}
        |   {head}:{joloom-aaj}
        |   {meat}:{tii7-iij}
        |   {nest}:{sook-aaj}
        |   {name}:{b'i'-aaj}
        |   {mother}:{naan-axeel}
        |   {younger.sibling}:{chaaq'-ixeel}
        |   {in.law}:{jii7-axeel}
        |   {husband}:{achajiil-oom}
        |   {wife}:{ixoqiil-oom}
        |   {daughter.in.law}:{aliib'-atz}
        |   {knee}:{ch'eek-aaj}
    ]
    ")":0
];

define N1sg [ N1pluralizable .o. [ "-" ?* -> 0 || _ .#. ]];

define N1pl [ N1pluralizable "+pl":0 ];

define N2poss [ N2 "+inal":0 ];

define N2plain [ N2 .o. [ "-" ?* -> 0 || _ .#. ]];

define N3poss [ N3 .o. [ "-" ?* -> 0 || _ .#. ]];

define N3unposs [ N3 "+unposs":0 ];


define NX [ 
    "N":0 "(":0
    [
        {house}:{jah}
    ]
    ")":0
];

define ADJ [
    "ADJ":0 "(":0
    [
            {big}:{nim<a>}
        |   {many}:{k'ih<a>}
    ]
    ")":0
];

define B [  
        "b1s+":{in-}
    |   "b2s+":{at-}
    |   "b3s+":{R-} 
    |   "b1p+":{oj-}
    |   "b2p+":{ix-}
    |   "b3p+":{e7-}
];

define M [ "go+" | "come+" ];

define A [ "a1s+" | "a2s+" | "a3s+" | "a1p+" | "a2p+" | "a3p+" ];

define Indicative [ "inc+" | "cpl+" | "n.imp+" | "dep+" ];

define Subjunctive [ "opt+" | "imp+" ];

define makeR [
        [{R-} -> 0          || _ [M|"a1s+"] ]
];

define makeVerbalSetA [
        ["a1s+" -> {en-}    // "go+" _ C ]
    .o. ["a1s+" -> {enw-}   // "go+" _ V ]
    .o. ["a1s+" -> {inw-}   || _ V ]
    .o. ["a1s+" -> {nu-}    // "come+" _ ]
    .o. ["a1s+" -> {in-}    || _ C ] 
    .o. ["a2s+" -> {a-}     || _ C ]
    .o. ["a2s+" -> {a-}     || _ C ]
    .o. ["a2s+" -> {aw-}    || _ V ]
    .o. ["a3s+" -> {u-}     || _ C ]
    .o. ["a3s+" -> {r-}     || _ V ] 
    .o. ["a1p+" -> {qa-}    || _ C ]
    .o. ["a1p+" -> {q-}     || _ V ] 
    .o. ["a2p+" -> {i-}     || _ C ]
    .o. ["a2p+" -> {iw-}    || _ V ] 
    .o. ["a3p+" -> {ki-}    || _ C ]
    .o. ["a3p+" -> {k-}     || _ V ] 
];

define makeNominalSetA [
        ["a1s+" -> {w-}     || _ V ]
    .o. ["a1s+" -> {nu-}    || _ C ]
    .o. makeVerbalSetA
];

define makeMovement [
        ["go+"  -> {e7-}    ]
    .o. ["come+"-> {ul-}    ]
];

define makeTAM [
        ["opt+" -> "dep+" ] .o. ["imp+" -> "inc+" ]
    .o. ["inc+" -> {ka-}    || _ [%0|%-|%R]* C ]
    .o. ["inc+" -> {k-}     || _ [%0|%-|%R]* V ]
    .o. ["n.imp+" -> {ma-}  || _ [%0|%-|%R]* C ]
    .o. ["n.imp+" -> {m-}   || _ [%0|%-|%R]* V ]
    .o. ["mov.imp+" -> {ja-}|| _ [%0|%-|%R]* C ]
    .o. ["mov.imp+" -> {j-} || _ [%0|%-|%R]* V ]
    .o. ["dep+" -> {cha-}   || _ [%0|%-|%R]* C ]
    .o. ["dep+" -> {ch-}    || _ [%0|%-|%R]* V ]
    .o. ["cpl+" -> {x-}     ]
];


define IVBase [
        IV
    |   IV "+celer":{-lob'}
    |   [RTV|DTV] "+cp":{-(V)taj}
    |   ADJ "+vers":{-Vr}
];

define DTVBase [
        DTV
    |   [RTV|DTV|IVBase] "+caus":{-(i)sa}
    |   [RTV|DTV|IVBase] "+instr":{-(i)b'e}
    |   RTV              "+celer":{-Vla7} # Handle these two separately
    |   DTV              "+celer":{-la7}  # to avoid getting the vowel
                                          # deleted.
    |   RTV              "+intens":{-V1a7}
    |   DTV              "+intens":{-1a7}
];

define PassiveStem [
        RTV "+pass":{-L} 
    |   [DTVBase .o. ~[?* {la7}]] "+pass":{-x}  
    |   [DTVBase .o.  [?* {la7}]] "+pass":{-ox}  
];
 
define IrregularAntipassiveStem [
        "RTV":0 "(":0   {buy}:{loq'}      ")":0 "+ap":{-oman}
    |   "RTV":0 "(":0   {wash}:{ch'aj}    ")":0 "+ap":{-oman}
    |   "RTV":0 "(":0   {help}:{tob'}     ")":0 "+ap":{-an}
];

define AntipassiveStem [
    IrregularAntipassiveStem
] .P. [
        RTV "+ap":{-On}  
    |   [DTVBase .o. ~[?* {la7}]] "+ap":{-n}  
    |   [DTVBase .o.  [?* {la7}]] "+ap":{-on}  
];



define IVStem [
        PassiveStem
    |   AntipassiveStem
    |   RTV "+af":{-Ow} 
    |   DTVBase "+af":{-n}
    |   IVBase
];

define DTVStem [ DTVBase ];

define VIIX [
        IVStem              "+viif":{-ik}   %# 
    |   IVStem              "+viin":0
    |   POS    "+iv":{-i7}  "+viif":{-k}    %#
    |   POS    "+iv":{-i7}  "+viin":0       
];

define VIDX [
        IVStem "+vidf":{-oq} %#
    |   [IVStem .o. [?* V]] "+vidn":{-oqa}
    |   [IVStem .o. [?* C]] "+vidn":{-a} 
];

define VTIX [
        RTV "+vtif":{-Oh} %#
    |   RTV "+vtin":0
    |   [DTVStem .o. ~[?* "7"]] ["+vtif":{-Vj} %# | "+vtin":{-j}]
    |   [DTVStem .o.  [?* "7"]] ["+vtif":0     %# | "+vtin":0   ]
];

define VTDX [
        RTV "+vtdf":{-A7} %#
    |   RTV "+vtdn":{-A}
    |   [DTVStem .o. ~[?* "7"]] ["+vtdf":{-Vj} %# | "+vtdn":{-j}]
    |   [DTVStem .o.  [?* "7"]] ["+vtdf":0     %# | "+vtdn":0   ]
];

define TV [
        Indicative      B   A VTIX
    |   Subjunctive     B   A VTDX
    |   Indicative      B M A VTDX
    |   "mov.imp+":{j-} B M A VTDX 
] .o. makeR .o. makeVerbalSetA .o. makeMovement .o. makeTAM;

define IV [
        Indicative      B     VIIX
    |   Subjunctive     B     VIDX
    |   Indicative      B M   VIDX
    |   "mov.imp+":{j-} B M   VIDX 
] .o. makeR .o. makeMovement .o. makeTAM;

# Noun stems. Here we import noun roots from the lexicon, but
# also handle nominalizations.
#
# IV bases (except for CP bases) need their verbal nouns
# lexically specified.  CP bases always have a VN in -eem.
# (Mondloch p.158).  All IV bases have a personal nominalization
# in -eel --- Mondloch says this is unproductive for the CP (p.159)
# so we overgenerate a bit.
#
# From active verbs we form a passive VN in -ik (p.132), an
# unpossessible antipassive VN in -ik (p.176), a possessible
# antipassive VN in -eem (p.176), and an antipassive personal
# nominalization in -eel (p.179).
#
# From RTV and DTV stems we generate an active perfect in -(OO)m.  
# We make this a noun that can go unpossessed --- thus accounting as
# well for the irregular *passive* perfect in -(OO)m.  From IV bases and
# antipassive stems we generate a perfect in -(i)naq; this can never
# be possessed.

define N1Stem [
        N1
    |   N1sg                        # Singulars of pluralizable N1
    |   N1pl                        # Plurals of pluralizable N1
    |   AntipassiveStem "+nm":{-eem} # Antipassive VN #1 (Mondloch p.176)
    |   AntipassiveStem "+nl":{-eel} ("+pl":{-aab'}) 
                                       # Antipassive agt N (p.179)
    |   IVBase "+nl":{-eel} ("+pl":{-aab'})           
                                        # Intrans  agt N (p.92)
    |   [$["+cp"] .o. IVBase] "+nm":{-eem} # CP VN (Mondloch p.158)
];

define N1aStem [
        N1a
    |   PassiveStem "+nk":{-ik}        # Passive VN (Mondloch p.132)
    |   [RTV|DTVStem|IVStem] "+nb":{-b'al}
                                       # Instrumental noun (Larsen 269)
];

define NXStem [
        NX
    |   AntipassiveStem "+nk":{-ik}    # Antipassive VN #2 (Mondloch p.176)
];

define Noun [
        (A) N1Stem                      "#"
    |    A  N1aStem "+poss":{-L}        "#"
    |       N1aStem                     "#"
    |       NXStem                      "#"
    |    A  N2poss                      "#"
    |   (A) N2plain                     "#"
    |    A  N3poss                      "#"
    |       N3unposs                    "#"
    |   (A) RTV     "+vtpf":{-OOm}      "#"
    |   (A) RTV     "+vtpn":{-Om}       
    |   (A) DTVStem "+vtpf":{-Vm}       "#"
    |   (A) DTVStem "+vtpn":{-m}       
    |       IVBase "+vipf":{-(i)naq}    "#" 
    |       IVBase "+vipn":{-(i)naq}   
    |   AntipassiveStem "+vipf":{-(i)naq} "#"
    |   AntipassiveStem "+vipn":{-(i)naq}
] .o. makeNominalSetA;

define Adjective [
            ADJ     ("+attr":{-V})
    |       ADJ     "+intens":{-Vlaj}
    |       ADJ     "+pl":{-aq}
    |       ADJ     "+pl.f":{-a'q}  "#"
    | [RTV|DTVStem] "+cp.stat":{-(V)tal}    ("+f":{-ik} "#")
    |       TEMP                            ("+f":{-ik} "#")
    |       PERM    "+sg":{-ik}
    |       PERM    "+pl":{-aq}
    |       PERM    "+pl.f":{-a'q}  "#"
];

define Word [TV | IV | Noun | Adjective];

regex Word;
save stack lex.bin
