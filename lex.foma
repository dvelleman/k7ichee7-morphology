define S ["-"|":"];
define V [ a | e | i | o | u | ä | ë | ï | ö | ü | ā | ē | ī | ō | ū ];
define X [ "A" | "O" | "V" ];
define C [ {b'} | {ch} | {ch'} | h | j | k | {k'} | l | m | n | p | q | {q'} | r | s
                | t | {t'} | {tz} | {tz'} | w | x | y | 7 ];



define RTV [
        "make":{b'an}
    |   "bury":{muq}
    |   "see":{il}
];

define DTV [
        "cure":{kuna}
    |   "carry":{eqa}
];

define IV [
        "enter":{ok}
    |   "walk":{b'ihn}
];

define N1 [
        "avocado":{ooj}
];

define NX [ 
        "house":{jah}
];

define ADJ [
        "big":{nim<a>}
];

define B [  
        "b1s+":{in-}
    |   "b2s+":{at-}
    |   "b3s+":{R-} 
    |   "b1p+":{oj-}
    |   "b2p+":{ix-}
    |   "b3p+":{e7-}
];

define M [ "go+" | "come+" ];

define A [ "a1s+" | "a2s+" | "a3s+" | "a1p+" | "a2p+" | "a3p+" ];

define Indicative [ "inc+" | "cpl+" | "n.imp+" | "dep+" ];

define Subjunctive [ "imp+" ];

define makeR [
        [{R-} -> 0          || _ [M|"a1s+"] ]
];

define makeVerbalSetA [
        ["a1s+" -> {en-}    // "go+" _ C ]
    .o. ["a1s+" -> {enw-}   // "go+" _ V ]
    .o. ["a1s+" -> {inw-}   || _ V ]
    .o. ["a1s+" -> {nu-}    // "come+" _ ]
    .o. ["a1s+" -> {in-}    || _ C ] 
    .o. ["a2s+" -> {a-}     || _ C ]
    .o. ["a2s+" -> {a-}     || _ C ]
    .o. ["a2s+" -> {aw-}    || _ V ]
    .o. ["a3s+" -> {u-}     || _ C ]
    .o. ["a3s+" -> {r-}     || _ V ] 
    .o. ["a1p+" -> {qa-}    || _ C ]
    .o. ["a1p+" -> {q-}     || _ V ] 
    .o. ["a2p+" -> {i-}     || _ C ]
    .o. ["a2p+" -> {iw-}    || _ V ] 
    .o. ["a3p+" -> {ki-}    || _ C ]
    .o. ["a3p+" -> {k-}     || _ V ] 
];

define makeNominalSetA [
        ["a1s+" -> {w-}     || _ V ]
    .o. ["a1s+" -> {nu-}    || _ C ]
    .o. makeVerbalSetA
];

define makeMovement [
        ["go+"  -> {e7-}    ]
    .o. ["come+"-> {ul-}    ]
];

define makeTAM [
        ["imp+" (->) "dep+" ] .o. ["imp+" -> "inc+" ]
    .o. ["inc+" -> {ka-}    || _ [%0|%-|%R]* C ]
    .o. ["inc+" -> {k-}     || _ [%0|%-|%R]* V ]
    .o. ["n.imp+" -> {ma-}  || _ [%0|%-|%R]* C ]
    .o. ["n.imp+" -> {m-}   || _ [%0|%-|%R]* V ]
    .o. ["mov.imp+" -> {ja-}|| _ [%0|%-|%R]* C ]
    .o. ["mov.imp+" -> {j-} || _ [%0|%-|%R]* V ]
    .o. ["dep+" -> {cha-}   || _ [%0|%-|%R]* C ]
    .o. ["dep+" -> {ch-}    || _ [%0|%-|%R]* V ]
    .o. ["cpl+" -> {x-}     ]
];



define IVBase [
        IV
    |   IV "+celer":{-lob'}
    |   [RTV|DTV] "+cp":{-(V)taj}
    |   ADJ "+vers":{-Vr}
];

define DTVBase [
        DTV
    |   [RTV|DTV|IVBase] "+caus":{-(i)sa}
    |   [RTV|DTV|IVBase] "+instr":{-(i)b'e}
    |   RTV              "+celer":{-Vla7} # Handle these two separately
    |   DTV              "+celer":{-la7}  # to avoid getting the vowel
                                          # deleted.
    |   RTV              "+intens":{-V1a7}
    |   DTV              "+intens":{-1a7}
];

define PassiveStem [
        RTV "+pass":{-L} 
    |   [DTVBase .o. ~[?* {la7}]] "+pass":{-x}  
    |   [DTVBase .o.  [?* {la7}]] "+pass":{-ox}  
];

define AntipassiveStem [
        RTV "+ap":{-On}  
    |   [DTVBase .o. ~[?* {la7}]] "+pass":{-n}  
    |   [DTVBase .o.  [?* {la7}]] "+pass":{-on}  
];


define IVStem [
        PassiveStem
    |   AntipassiveStem
    |   RTV "+af":{-Ow} 
    |   DTVBase "+af":{-n}
    |   IV 
];

define DTVStem [ DTVBase ];

define VIIX [
        IVStem "+ss":{-ik#} 
    |   IVStem 
];

define VIDX [
        IVStem "+ss":{-oq#}
    |   [IVStem .o. [?* V]] "+ss":{-oqa}
    |   [IVStem .o. [?* C]] "+ss":{-a} 
];

define VTIX [
        RTV "+ss":{-Oh#}
    |   RTV
    |   [DTVStem .o. ~[?* "7"]] ["+ss":{-:j#} | "+ss":{-j}]
    |   [DTVStem .o.  [?* "7"]]
];

define VTDX [
        RTV "+ss":{-A7#}
    |   RTV "+ss":{-A}
    |   [DTVStem .o. ~[?* "7"]] ["+ss":{-:j#} | "+ss":{-j}]
    |   [DTVStem .o.  [?* "7"]]
];

define TV [
        Indicative      B   A VTIX
    |   Subjunctive     B   A VTDX
    |   Indicative      B M A VTDX
    |   "mov.imp+":{j-} B M A VTDX 
] .o. makeR .o. makeVerbalSetA .o. makeMovement .o. makeTAM;

define IV [
        Indicative      B     VIIX
    |   Subjunctive     B     VIDX
    |   Indicative      B M   VIDX
    |   "mov.imp+":{j-} B M   VIDX 
] .o. makeR .o. makeMovement .o. makeTAM;

# Noun stems. Here we import noun roots from the lexicon, but
# also handle nominalizations.
#
# IV bases (except for CP bases) need their verbal nouns
# lexically specified.  CP bases always have a VN in -eem.
# (Mondloch p.158).  All IV bases have a personal nominalization
# in -eel --- Mondloch says this is unproductive for the CP (p.159)
# so we overgenerate a bit.
#
# From active verbs we form a passive VN in -ik (p.132), an
# unpossessible antipassive VN in -ik (p.176), a possessible
# antipassive VN in -eem (p.176), and an antipassive personal
# nominalization in -eel (p.179).
#
# From RTV and DTV stems we generate an active perfect in -(OO)m.  
# We make this a noun that can go unpossessed --- thus accounting as
# well for the irregular *passive* perfect in -(OO)m.  From IV bases and
# antipassive stems we generate a perfect in -(i)naq; this can never
# be possessed.

define N1Stem [
        N1
    |   RTV     "+perf":{-OOm}       # Active perfect (RTV) 
                                     #   and irreg. passive perfect.
    |   DTVStem "+perf":{-:m}        # Active perfect (DTV)
                                     #   and irreg. passive perfect.
    |   AntipassiveStem "+vn":{-eem} # Antipassive VN #1 (Mondloch p.176)
    |   AntipassiveStem "+person":{-eel} # Antipassive agt N (p.179)
    |   IVBase "+person":{-eel}            # Intrans  agt N (p.92)
    |   [$["+cp"] .o. IVBase] "+vn":{-eem} # CP VN (Mondloch p.158)
];

define N1aStem [
        PassiveStem "+vn":{-ik}        # Passive VN (Mondloch p.132)
];

define NXStem [
        NX
    |   AntipassiveStem "+vn":{-ik}    # Antipassive VN #2 (Mondloch p.176)
    |   IVBase "+perf":{-(i)naq}       # Intransitive perfect
    |   AntipassiveStem "+perf":{-(i)naq}   # Antipassive perfect
];

define Noun [
        (A) N1Stem                  "#"
    |    A  N1aStem "+poss":{-L}    "#"
    |       N1aStem                 "#"
    |       NXStem                  "#"
] .o. makeNominalSetA;

define Word [TV | IV | Noun];

regex Word;
save stack lex.bin
